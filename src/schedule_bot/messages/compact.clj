(ns schedule-bot.messages.compact
  (:require [java-time.api :as jt]
            [schedule-bot.messages.markup :as markup]))

(defn- get-day-of-week [date]
  (condp #(%1 %2) date
    jt/monday? "ĞŸĞ¾Ğ½ĞµĞ´ĞµĞ»ÑŒĞ½Ğ¸Ğº"
    jt/tuesday? "Ğ’Ñ‚Ğ¾Ñ€Ğ½Ğ¸Ğº"
    jt/wednesday? "Ğ¡Ñ€ĞµĞ´Ğ°"
    jt/thursday? "Ğ§ĞµÑ‚Ğ²ĞµÑ€Ğ³"
    jt/friday? "ĞŸÑÑ‚Ğ½Ğ¸Ñ†Ğ°"
    jt/saturday? "Ğ¡ÑƒĞ±Ğ±Ğ¾Ñ‚Ğ°"
    jt/sunday? "Ğ’Ğ¾ÑĞºÑ€ĞµÑĞµĞ½ÑŒĞµ"))

(defn- get-day-line [date today]
  (let [tomorrow (jt/plus today (jt/days 1))
        label (condp = date
                today " (Ğ¡ĞµĞ³Ğ¾Ğ´Ğ½Ñ)"
                tomorrow " (Ğ—Ğ°Ğ²Ñ‚Ñ€Ğ°)"
                "")]
    (-> (markup/start)
        (markup/text "ğŸ—“ ")
        (markup/bold (get-day-of-week date))
        (markup/italic label)
        (markup/text "\n"))))

(defn- get-class-line [{name :name place :place group :group remote? :remote? [start _] :time}]
  (-> (markup/start)
      (markup/mono (if remote? "Ğ´Ğ¸ÑÑ‚." (jt/format "HH:mm" start)))
      (markup/text " | ")
      (markup/text (if (some? group) (str "[" group "] ") ""))
      (markup/text name)
      (#(if (some? place)
          (-> %
              (markup/text ", ")
              (markup/italic place))
          %))
      (markup/text "\n")))

(defn- try-get-class-line [class]
  (try
    (get-class-line class)
    (catch Exception _
      (-> (markup/start)
          (markup/italic "âš ï¸ fail to parse class\n")))))

(defn- get-day [{classes :classes day :day} today]
  (let [class-lines (->> classes
                         (filter #(not (:empty %)))
                         (sort-by #(or (:group %) 0) <)
                         (sort-by #(-> % :time first) jt/<)
                         (map try-get-class-line))]
    (markup/combine
     (get-day-line day today)
     (apply markup/combine class-lines)
     (markup/start "\n"))))

(defn- get-link-line [link]
  (-> (markup/start)
      (markup/text "ğŸŒ ")
      (markup/link "herzen.spb.ru" link)))

(defn create [days today link]
  (markup/combine
   (apply markup/combine (map #(get-day % today) (sort-by :day jt/< days)))
   (get-link-line link)))

